archetype (adl_version=1.4; uid=135fe2ef-6379-4c38-9ef2-6ec4e2d2eda9)
	openEHR-EHR-OBSERVATION.news_hse.v0

concept
	[at0000]	-- National Early Warning Score (HSE)
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Hildegard Franke">
		["organisation"] = <"freshEHR Clinicial Informatics Ltd.">
		["email"] = <"hildi@freshehr.com">
		["date"] = <"2016-02-20">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record provide a composite score based on categorising physiological readings and observations, as a simple method to support objective assessment of the degree of illness in an unwell patient over the age of 16 years.">
			use = <"Use to record a composite score to support assessment of the degree of illness in an unwell patient, over the age of 16 years. 
">
			keywords = <"warning", "AVPU", "RR", "HR", "BP", "Temp", "SBP", "triage", "NEWS", "Sats">
			misuse = <"Not to be used to record each physical measurement or clinical observation. Use specific OBSERVATION archetypes for this purpose - OBSERVATION.blood_pressure, OBSERVATION.heart_rate, OBSERVATION.respirations, OBSERVATION.body_temperature, OBSERVATION.indirect_oximetry, OBSERVATION.avpu and OBSERVATION.glasgow_coma.

Not to be used for children/teens under the age of 16 years or in pregnancy.">
			copyright = <"© Clinical Models UK">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Heather Leslie, Ocean Informatics, Australia", "Ian McNicoll, freshEHR Clinical Informatics Ltd, UK">
	other_details = <
		["current_contact"] = <"Hildegard Franke">
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["references"] = <"Health Service Executive Ireland (HSE)  http://www.hse.ie/eng/about/Who/ONMSD/Practicedevelopment/MEWS/

">
		["build_uid"] = <"be6a0238-d1cb-4f44-895d-6db12baa7e40">
		["original_publisher"] = <"UK Clinical Models">
		["original_namespace"] = <"uk.org.clinicalmodels">
		["MD5-CAM-1.0.1"] = <"27561A19A33DD2558AD4BC5DFFA5CFEE">
		["custodian_namespace"] = <"uk.org.clinicalmodels">
		["custodian_organisation"] = <"UK Clinical Models">
	>

definition
	OBSERVATION[at0000] matches {	-- National Early Warning Score (HSE)
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..1} matches {	-- Point in time
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0006] occurrences matches {0..1} matches {	-- Respiration Rate
										value matches {
											0|[local::at0018], 	-- 12-20
											1|[local::at0019], 	-- 9-11
											2|[local::at0020], 	-- 21-24
											3|[local::at0021]  	-- <=8 or >=25
										}
									}
									ELEMENT[at0029] occurrences matches {0..1} matches {	-- Oxygen Saturation
										value matches {
											0|[local::at0030], 	-- >= 96
											1|[local::at0031], 	-- 94-95
											2|[local::at0032], 	-- 92-93
											3|[local::at0033]  	-- <=91
										}
									}
									ELEMENT[at0034] occurrences matches {0..1} matches {	-- Inspired Oxygen
										value matches {
											0|[local::at0036], 	-- No
											3|[local::at0037]  	-- Yes
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {	-- Temperature
										value matches {
											0|[local::at0022], 	-- 36.1-38.0
											1|[local::at0023], 	-- 35.1-36.0 or 38.1-39.0
											2|[local::at0038], 	-- >=39.1
											3|[local::at0039]  	-- <=35.0
										}
									}
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Systolic Blood Pressure
										value matches {
											0|[local::at0014], 	-- 111-249
											1|[local::at0015], 	-- 101-110 or >= 250
											2|[local::at0016], 	-- 91-100
											3|[local::at0017]  	-- <=90
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Heart Rate
										value matches {
											0|[local::at0013], 	-- 51-90
											1|[local::at0012], 	-- 41-50 or 91-110
											2|[local::at0011], 	-- 111-130 or <=40
											3|[local::at0010]  	-- >=131
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {	-- Level of consciousness
										value matches {
											0|[local::at0024], 	-- A
											3|[local::at0025]  	-- V, P or U
										}
									}
									ELEMENT[at0040] occurrences matches {0..1} matches {	-- Total score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..21|}
											}
										}
									}
									ELEMENT[at0049] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"National Early Warning Score (HSE)">
					description = <"A simple score used to provide an objective indication of the degree of illness of a patient. This version follows guidance issued by the HSE Ireland. ">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Point in time">
					description = <"A specific date and/or time which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Systolic Blood Pressure">
					description = <"Range category for the systolic blood pressure (SBP) measurement of a patient.">
				>
				["at0005"] = <
					text = <"Heart Rate">
					description = <"Range category for the heart rate (HR) measurement of a patient.">
				>
				["at0006"] = <
					text = <"Respiration Rate">
					description = <"Range category for the respiratory rate (RR) measurement of a patient.">
				>
				["at0007"] = <
					text = <"Temperature">
					description = <"Range category for the body temperature (Temp) measurement of a patient.">
				>
				["at0008"] = <
					text = <"Level of consciousness">
					description = <"Observed category for the AVPU scale rating of a patient's conscious state.">
				>
				["at0010"] = <
					text = <">=131">
					description = <"The heart rate measurement is less than/equal to 40 beats/min or greater than/equal to 131 beats/min; scored as 3 points.">
				>
				["at0011"] = <
					text = <"111-130 or <=40">
					description = <"The heart rate measurement is between 111 and 130 beats/min; scored as 2 points.">
				>
				["at0012"] = <
					text = <"41-50 or 91-110">
					description = <"The heart rate measurement is between 41 and 50 beats/min or between 91 and 110 beats/min; scored as 1 point.">
				>
				["at0013"] = <
					text = <"51-90">
					description = <"The heart rate measurement is between 51 and 90 beats/min; scored as 0 points.">
				>
				["at0014"] = <
					text = <"111-249">
					description = <"The systolic blood pressure measurement is between 111 and 219 mmHg; scored as 0 points.">
				>
				["at0015"] = <
					text = <"101-110 or >= 250">
					description = <"The systolic blood pressure measurement is between 101 and 110 mmHg; scored as 1 point.">
				>
				["at0016"] = <
					text = <"91-100">
					description = <"The systolic blood pressure measurement is between 91 and 100 mmHg; scored as 2 points.">
				>
				["at0017"] = <
					text = <"<=90">
					description = <"The systolic blood pressure measurement is less than/equal to 90 mmHg or greater than/equal to 220 mmHg; scored as 3 points.">
				>
				["at0018"] = <
					text = <"12-20">
					description = <"The respiratory rate measurement is between 12 and 20 breaths/min; scored as 0 points.">
				>
				["at0019"] = <
					text = <"9-11">
					description = <"The respiratory rate measurement is between 9 and 11 breaths/min; scored as 1 point.">
				>
				["at0020"] = <
					text = <"21-24">
					description = <"The respiratory rate measurement is between 21 and 24 breaths/min; scored as 2 points.">
				>
				["at0021"] = <
					text = <"<=8 or >=25">
					description = <"The respiratory rate measurement is less than/equal to 8 breaths/min or greater than/equal to 25 breaths/min; scored as 3 points.">
				>
				["at0022"] = <
					text = <"36.1-38.0">
					description = <"The body temperature measurement is between 36.1 and 38.0 degrees Celsius; scored as 0 points.">
				>
				["at0023"] = <
					text = <"35.1-36.0 or 38.1-39.0">
					description = <"The body temperature measurement is between 35.1 and 36.0 degrees Celsius or between 38.1 and 39.0 degrees Celsius; scored as 2 points.">
				>
				["at0024"] = <
					text = <"A">
					description = <"The patient is alert or awake; scored as 0 points.">
				>
				["at0025"] = <
					text = <"V, P or U">
					description = <"The patient responds only to verbal, pain or stimulus; scored as 3 points.">
				>
				["at0029"] = <
					text = <"Oxygen Saturation">
					description = <"Range category for the oxygen saturation measurement of a patient.">
				>
				["at0030"] = <
					text = <">= 96">
					description = <"The oxygen saturation level is greater than/equal to 96%;scored as 0 points.">
				>
				["at0031"] = <
					text = <"94-95">
					description = <"The oxygen saturation level is between 94% and 95%; scored as 1 point.">
				>
				["at0032"] = <
					text = <"92-93">
					description = <"The oxygen saturation level is between 92% and 93%; scored as 2 points.">
				>
				["at0033"] = <
					text = <"<=91">
					description = <"The oxygen saturation level is less than/equal to 91%; scored as 3 points.">
				>
				["at0034"] = <
					text = <"Inspired Oxygen">
					description = <"Range category depending on whether the patient is receiving inspired oxygen.">
				>
				["at0036"] = <
					text = <"No">
					description = <"The patient is not receiving supplemental oxygen; scored as 0.">
				>
				["at0037"] = <
					text = <"Yes">
					description = <"The patient is receiving supplemental oxygen; scored as 1.">
				>
				["at0038"] = <
					text = <">=39.1">
					description = <"The body temperature is greater than/equal to 39.1 degrees Celcius; scored as 2 points.">
				>
				["at0039"] = <
					text = <"<=35.0">
					description = <"The body temperature is less than/equal to 35 degrees Celcius ; scored as 3 points.">
				>
				["at0040"] = <
					text = <"Total score">
					description = <"Total NEWS score">
				>
				["at0049"] = <
					text = <"Comment">
					description = <"*">
				>
			>
		>
	>
