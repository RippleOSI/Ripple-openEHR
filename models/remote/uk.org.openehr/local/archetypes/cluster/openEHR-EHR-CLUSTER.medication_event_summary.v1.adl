archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.medication_event_summary.v1

concept
	[at0000]	-- Medication event summary
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"Ocean Informatics, UK">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2012-12-12">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a summary of a medication course over time, including key dates and details of issues, repeat authorisations, dispensing and administration where appropriate. This is most often used when a summary extract is required, such as an unscheduled care summary or for referral and discharge documentation.">
			use = <"Used with appropriately filled slots to record details of specific medication events such as issues, repeat authorisations, dispensing or administration events where required.">
			keywords = <"medication", "supply", "recommendation", "prescription">
			misuse = <"Not to be used to record the original individual medication records in a native application or shared medication database, or where a full, faithful 'event-based' extract is required for transfer to another system e.g. a GP2GP type transfer.">
			copyright = <"© Clinical Models UK">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Marcus Baw, openGPSoC / Baw Medical Ltd, United Kingdom (Editor)", "Leo Fogarty, SCIMP/HSCIC, United Kingdom (Editor)", "RCP Health Informatics Unit, UK", "Dr John Williams, GP2GP project, UK", "Dr Leo Fogarty, GP2GP project, UK", "Ian McNicoll, Ocean Informatics, United Kingdom (Editor)", "Paul Miller, SCIMP, United Kingdom (Editor)", "Ian Thompson, SCIMP, United Kingdom (Editor)", "John Williams, GP2GP, United Kingdom (Editor)">
	other_details = <
		["references"] = <"NHS England Messaging Implementation Manual (GP2GP messages) http://www.uktcregistration.nss.cfh.nhs.uk/trud3/user/guest/group/0/pack/11
NHS Scotland SCI-XML (SCI-Gateway, ECS, ePharmacy)  http://www.sci.scot.nhs.uk/products/gateway/gate_down_xml.htm
NHS Wales Informatics Service, Integrated Health Record http://www.wales.nhs.uk/nwis/page/52553
DM+D drug dictionary: http://www.dmd.nhs.uk/
Royal College of Physicians, Core Clinical Headings, http://www.rcplondon.ac.uk/projects/standards-core-clinical-information">
		["MD5-CAM-1.0.1"] = <"59FE40222ED03E662DFD9CA1251ABEEC">
	>

definition
	CLUSTER[at0000] matches {	-- Medication event summary
		items cardinality matches {1..*; unordered} matches {
			CLUSTER[at0070] occurrences matches {0..*} matches {	-- Medication event
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0047] occurrences matches {0..1} matches {	-- Event date
						name matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0067, 	-- Date ordered/recommended
									at0059, 	-- Date first prescription issued
									at0060, 	-- Date last prescription issued
									at0061, 	-- Date first authorised
									at0062, 	-- Date last authorised
									at0063, 	-- Date first dispensed
									at0064, 	-- Date last dispensed
									at0065, 	-- Date first administered
									at0066, 	-- Date last administered
									at0072, 	-- Date discontinued
									at0073, 	-- Date authorised
									at0074, 	-- Date prescription issued
									at0075, 	-- Date dispensed
									at0076, 	-- Date administered
									at0077, 	-- Date administration withheld
									at0078, 	-- Date reviewed
									at0079, 	-- Date last reviewed
									at0080]	-- Date changed
								}
							}
						}
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0038] occurrences matches {0..1} matches {	-- Medication event detail
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.medication_event(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0071] occurrences matches {0..1} matches {	-- Link to event record
						value matches {
							DV_EHR_URI matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Medication event summary">
					description = <"Summary of key medication events and dates, normally carried within a Medication Statement Entry.">
				>
				["at0038"] = <
					text = <"Medication event detail">
					description = <"Further detail about the medication event.">
				>
				["at0047"] = <
					text = <"Event date">
					description = <"A date relating to a specific medication event during the course of a medication. Generally only exposed when the medication order archetype is being used in 'summary' mode.">
				>
				["at0059"] = <
					text = <"Date first prescription issued">
					description = <"The date at which the medication was first issued. 'Issued' refers to the prescription 'token' electronic or paper which authorises supply of a medication.">
				>
				["at0060"] = <
					text = <"Date last prescription issued">
					description = <"The date at which the medication prescription was last issued. This refers to the prescription 'token' electronic or paper which authorises supply of a medication.">
				>
				["at0061"] = <
					text = <"Date first authorised">
					description = <"The date at which the medication was first authorised.For a repeat prescription, authorisation refers to the creation of the repeat prescription 'master' which is followed by the production of one or more prescription issues.">
				>
				["at0062"] = <
					text = <"Date last authorised">
					description = <"The data at which the medication was last authorised.The date at which the medication was first authorised.For a repeat prescription, authorisation refers to the creation of the repeat prescription 'master' which is followed by the production of one or more prescription issues. Authorisation is generally only given for a limited period or limited number of issues, after which re-authorisation is required.">
				>
				["at0063"] = <
					text = <"Date first dispensed">
					description = <"The date at which the medicaton was first physically dispensed.">
				>
				["at0064"] = <
					text = <"Date last dispensed">
					description = <"The date at which the medication was last dispensed.">
				>
				["at0065"] = <
					text = <"Date first administered">
					description = <"The date at which the medication was first administered to the patient.">
				>
				["at0066"] = <
					text = <"Date last administered">
					description = <"The date at which the medication was last administered.">
				>
				["at0067"] = <
					text = <"Date ordered/recommended">
					description = <"The data at which the medication course was first ordered or recommended.">
				>
				["at0070"] = <
					text = <"Medication event">
					description = <"Summary of a medication event. Often this will be simply one or more key dates but further event detail may be required in some circumstances.">
				>
				["at0071"] = <
					text = <"Link to event record">
					description = <"A link to the original record of the medication event.">
				>
				["at0072"] = <
					text = <"Date discontinued">
					description = <"The date at which the medication was discontinued.">
				>
				["at0073"] = <
					text = <"Date authorised">
					description = <"The date at which the medication was authorised or re-authorised.">
				>
				["at0074"] = <
					text = <"Date prescription issued">
					description = <"The date at which a medication prescription was issued i.e the physical or electronic prescription token was created.">
				>
				["at0075"] = <
					text = <"Date dispensed">
					description = <"The date at which a medication was dispensed.">
				>
				["at0076"] = <
					text = <"Date administered">
					description = <"The date at which a medication was administered.">
				>
				["at0077"] = <
					text = <"Date administration withheld">
					description = <"The data at which administration of a medication was withheld or suspended.">
				>
				["at0078"] = <
					text = <"Date reviewed">
					description = <"The date at which the medication order was reviewed.">
				>
				["at0079"] = <
					text = <"Date last reviewed">
					description = <"The date at which the medication order was last reviewed.">
				>
				["at0080"] = <
					text = <"Date changed">
					description = <"The date at which the medication instruction was modified.">
				>
			>
		>
	>
